# ASMinit.def: Determine the ASM Home Location and System Identifier
# $Id: ASMinit.def,v 1.5 2012/01/03 13:34:41 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/ASMinit.def,v 1.5 2012/01/03 13:34:41 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

ASMinit - Determine the ASM Home Location and System Identifier

=head1 DESCRIPTION

This module tries to determine the ASM home directory and the ASM system
identifier in a cluster, using the following commands:

=over 3

=item o C<srvctl config asm>

=item o C<srvctl config asm -n E<lt>nodenameE<gt>>

=back

When the system identifier cannot be determined using those commands, it uses
F<crs_stat> to get it.

=cut

# Make the module persistent
keep $KEEP_BLOCK

# Define the macro to determine the ASM home and SID
macro get_asm
{var ($crs,$hom) = (${CLUSTER_CRS_HOME})

 if !?$crs
  run CRSinit(\$crs)
 if testDir('d',$crs)
 {# Try to determine the ASM home
  var $cmd = catCommand($crs,'bin','srvctl')
  if grepCommand(concat($cmd,' config asm'),\
                 '^(PRK[A-Z]\-\d{4}|\s*\[)','v')
  {loop $lin (last)
   {next !match($lin,'^ASM home:\s+(.*?)\s*$')
    if testDir('d',catDir(last))
    {var $hom = lastDir()
     break
    }
   }
  }
  elsif grepCommand(concat($cmd,' config asm -n ',quote(${RDA.NODE})),\
                    '^(PRK[A-Z]\-\d{4}|\s*\[)','fv')
  {var ($sid,$dir) = split('\s+',last,2)
   if testDir('d',catDir($dir))
    return [lastDir(),$sid]
  }

  # Try to resolve the ASM SID
  var $pat = concat('\.',quote(${RDA.NODE}),'\.([^\.]+)\.asm$')
  loop $lin (grepCommand(catCommand($crs,'bin','crs_stat'),'\.asm$'))
  {if match($lin,$pat)
    return [$hom,concat('+',last)]
  }
 }

 # Give up
 return [$hom,'+ASM']
}

# Determine the ASM home and SID on first call
var ($asm,\$hom,\$sid) = (${REG.ASM:get_asm()},@arg)
if ?$asm->[0]
 var $hom = last
if ?$asm->[1]
 var $sid = last

=head1 SEE ALSO

L<CRSinit.def|modules::CRSinit>

=begin credits

=over 10

=item RDA 4.19: Jaime Alcoreza.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
