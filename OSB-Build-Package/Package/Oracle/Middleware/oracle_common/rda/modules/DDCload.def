# DDCload.def: Collects Oracle Database Diagnostic Collector Reports
# $Id: DDCload.def,v 2.5 2012/01/03 13:34:41 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/DDCload.def,v 2.5 2012/01/03 13:34:41 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

DDCload - Collects Oracle Database Diagnostic Collector Reports

=head1 DESCRIPTION

=head2 oraddc - Database Diagnostic Collector

This module gets reports generated by Oracle Database Diagnostic Collector
(ORADDC) during the last 15 days.

=cut

# Initialisation
var @tb_mon = (\
  '','Jan','Feb','Mar','Apr','May','Jun','Jul','Aug','Sep','Oct','Nov','Dec')
var %tb_dmp = ('A','Hang&nbsp;Analyze&nbsp;Dump',\
               'C','Locking&nbsp;Contention&nbsp;Report',\
               'D','Active&nbsp;Session&nbsp;History&nbsp;Dump',\
               'E','Event&nbsp;Histogram',\
               'H','Shared&nbsp;Pool&nbsp;(Heap)&nbsp;Dump',\
               'L','Library&nbsp;Cache&nbsp;Dump',\
               'R','Row&nbsp;Cache&nbsp;Dump',\
               'S','System&nbsp;State&nbsp;Dump',\
               'W','Session&nbsp;Wait&nbsp;Events&nbsp;Report')
var %tb_met = ('-','None',\
               'B','Blocking&nbsp;Sessions',\
               'E','Wait&nbsp;Event&nbsp;Sessions',\
               'L','Listener&nbsp;Names',\
               'O','Oracle&nbsp;Process&nbsp;Identifiers',\
               'P','Process&nbsp;Identifiers',\
               'S','Session&nbsp;Identifiers',\
               'U','Session&nbsp;User')
var %tb_prc = ('E','Process&nbsp;Error&nbsp;Stack',\
               'F','Process&nbsp;Open&nbsp;Files',\
               'L','Process&nbsp;Dynamic&nbsp;Libraries',\
               'O','Operating&nbsp;System&nbsp;Trace',\
               'P','Process&nbsp;State&nbsp;Dump',\
               'S','Process&nbsp;Stack',\
               'T','10046&nbsp;Trace')

# Collect recent files
debug ' Inside LOAD module, gathering ORADDC reports'
report oraddc
prefix
{write '---+ Oracle Database Diagnostic Collector (ORADDC) Results'
 write 'Limited to ORADDC runs done in last 15 days%BR%&nbsp;'
 write '|*Date*|*Run%BR%Options*|*Background%BR%Process*|*Process%BR%Method*|\
         *Process%BR%Arguments*|*Process%BR%Reports*|*Reports*|\
         *Time%BR%Constants*|'
}
loop $fil (grepDir(${OUT.E},concat('^',${CUR.GROUP},'_DDC_\d{10}_log\.txt$'),\
                   'iptm15'))
{# Decode the ORADDC options
 next !grepFile($fil,'ORADDC Options','f')
 var ($opt,@dmp,@prc) = (field('\|',2,last))
 var @opt = split(':',replace($opt,'&#58;',':',true))
 var ($met,$arg) = split('\/',$opt[2])
 var $arg = replace($arg,',',', ',true)
 if !length($met)
  var $met = '-'
 loop $itm (split(',',trim($opt[3])))
 {if $itm
   var @prc = (@prc,nvl($tb_prc{$itm},$itm))
 }
 loop $itm (split(',',trim($opt[4])))
 {if $itm
   var @dmp = (@dmp,nvl($tb_dmp{$itm},$itm))
 }
 if isFiltered()
  var $opt = replace($opt,':','&#58;',true)

 # Decode the date
 var ($yea,$mon,$day,$hou,$min) = \
  match($fil,'_DDC_(\d\d)(\d\d)(\d\d)(\d\d)(\d\d)_log\.txt$',true)
 incr $yea,2000

 # Add the log in the report
 write '|[[extern/',basename($fil,'.txt'),'.htm][_blank][',\
       $day,'-',$tb_mon[$mon],'-',$yea,'&nbsp;',$hou,':',$min,']]|',\
       $opt,' |',replace($opt[1],',','%BR%',true),' |',\
       nvl($tb_met{$met},$met),' |',$arg,' |',\
       join('%BR%',@prc),' |',join('%BR%',@dmp),' |',$opt[5],' |'
}
if isCreated(true)
 toc '2:[[',getFile(),'][rda_report][Database Diagnostic Collector]]'

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
