# M242F15N.def: Collects Network Driver Parameters in Sun Fire 15K Servers
# $Id: M242F15N.def,v 1.3 2012/05/08 04:36:04 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M242F15N.def,v 1.3 2012/05/08 04:36:04 mschenke Exp $
#
# Change History
# 20120508  MSC  Apply naming conventions.

=head1 NAME

M242F15N - Collects Network Device Driver Parameters in Sun Fire 15K Servers

=head1 DESCRIPTION

This module collects network driver parameters for the C<scman> and C<dman>
devices in Sun Fire 15K servers.

=cut

use Mrc

# Initialization
var $VALIDATE = true
keep $VALIDATE

section begin

var $ERR = '---## Associated Errors'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

run XPLRlib('sf15k_ndd')

# -----------------------------------------------------------------------------
# XPLR_f15n section
# -----------------------------------------------------------------------------

section XPLR_f15n

# Validate the execution context
if !${XPLR_GLOBAL:true}
 return
call log_run('Processing F15N sections ...')

=head2 f15n_ndd - Network Device Driver Parameters (Sun Fire 15K Servers)

Gathers the network device driver information using the following commands:

=over 2

=item o C</usr/sbin/ndd /dev/${MOD} \?>

=item o C</usr/sbin/ndd /dev/${MOD} ${PARAM}>

=back

=cut

debug ' Inside F15N collection, gathering F15N information'

# Determine the commands to collect
var @cmd = ()
loop $dev ('/dev/dman','/dev/scam')
{next !testFile('r',$dev)
 var $mod = substr($dev,5)
 call push(@cmd,\
   [concat('sf15k/ndd/',$mod,'/',$mod,'.list'),\
    '/usr/sbin/ndd',concat($dev,' \?'),\
    concat('---+ Parameters Supported by ',$dev,' Driver')])
 loop $lin (grepCommand(concat('/usr/sbin/ndd ',$dev,' \?'),'\(read'))
 {var $par = field('\s+',0,$lin)
  next compare('eq',$par,'?')
  call push(@cmd,\
    [concat('sf15k/ndd/',$mod,'/',$par),\
     '/usr/sbin/ndd',concat($dev,' ',$par),\
     concat('---++ Value for ',$par,' Parameter in ',$dev,' Driver')])
 }
}

# Collect the information
report f15n
title '---+!! Network Device Driver Parameters (Sun Fire 15K Servers)'
title $TOC
call do_exec(@cmd)
if isCreated(true)
 toc '2:[[',getFile(),\
     '][rda_report][Network Device Driver Parameters (Sun Fire 15K Servers)]]'

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
