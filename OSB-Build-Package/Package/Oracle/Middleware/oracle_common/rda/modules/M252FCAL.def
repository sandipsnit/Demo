# M252FCAL.def: Collects Fiber Channel Arbitrated Loop Drives Information
# $Id: M252FCAL.def,v 1.3 2012/03/30 17:34:42 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M252FCAL.def,v 1.3 2012/03/30 17:34:42 mschenke Exp $
#
# Change History
# 20120330  MSC  Remove trailing spaces.

=head1 NAME

M252FCAL - Collects Fiber Channel Arbitrated Loop Drives Information

=head1 DESCRIPTION

This module collects disk information on internal Fiber Channel Arbitrated
Loop (FCAL) drives.

=cut

use Mrc

# Initialization
var $VALIDATE = true
keep $VALIDATE

section begin

var $ERR = '---## Associated Errors'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

run XPLRlib('fcal')

#------------------------------------------------------------------------------
# XPLR_fcal section
#------------------------------------------------------------------------------

section XPLR_fcal

# Validate the execution context
if !${XPLR_GLOBAL:true}
 return
call log_run('Processing FCAL sections ...')

=head2 fcal - Fiber Channel Arbitrated Loop Drives Information

Gathers the internal Fiber Channel Arbitrated Loop drives information using the
C</usr/sbin/luxadm display ${LUN}> command. The existing ${LUN} are extracted
from the output of the C</usr/sbin/luxadm probe> command.

=cut

debug ' Inside FCAL collection, gathering the FCAL information'
report fcal
title '---+!! Fiber Channel Arbitrated Loop Drives'
title $TOC

var %lun = ()
loop $lun (grepCommand('/usr/sbin/luxadm probe','WWN:(\S+)\s','1'))
 var $lun{$lun} = 1
loop $lun (keys(%lun))
{var $cmd = concat('/usr/sbin/luxadm display ',$lun)
 prefix
 {write '---+ Drive Information for World Wide Name:',$lun
  write '---## Using: ',encode($cmd)
 }
 sleep
 call collectCommand({nam=>concat('disks/luxadm_display_',$lun),\
   out=>{blk=>true,flt=>true,idx=>true,rpt=>${CUR.REPORT}},\
   err=>{blk=>true,flt=>true,hdr=>$ERR,rpt=>${CUR.REPORT}}\
   },$cmd)
 if hasOutput(true)
  write $TOP
}
if isCreated(true)
 toc '2:[[',getFile(),'][rda_report][Fiber Channel Arbitrated Loop Drives]]'

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
