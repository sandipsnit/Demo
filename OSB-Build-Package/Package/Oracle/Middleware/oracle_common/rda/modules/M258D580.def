# M258D580.def: Collects StorageTek 5800 System Information
# $Id: M258D580.def,v 1.4 2012/05/08 04:36:04 mschenke Exp $
# $ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M258D580.def,v 1.4 2012/05/08 04:36:04 mschenke Exp $
#
# Change History
# 20120507  MSC  Apply naming conventions.

=head1 NAME

M258D580 - Collects StorageTek 5800 System Information

=head1 DESCRIPTION

Collects StorageTek 5800 system information.

=cut

use Mrc

# Initialization
var $VALIDATE = true
keep $VALIDATE

section begin

var $ERR = '---## Associated Errors'
var $TOP = '[[#Top][Back to top]]'

run XPLRlib('st5800')

#------------------------------------------------------------------------------
# XPLR_st5800 section
#------------------------------------------------------------------------------

section XPLR_st5800

if !${XPLR_GLOBAL:true}
 return
call log_run('Processing D580 sections ...')

=head2 StorageTek 5800 Information

Collects the Golden Snapshot files present in the F</var/opt/STORtools/logs>
directory using the F</var/adm/extractor/honeycomb_extract> tool.

=cut

debug ' Inside ST5800 collection, gathering the ST5800 information'
if testFile('x',$cmd = '/opt/honeycomb/extractor/extractor.pl')
{pretoc '2: StorageTek 5800'

 # Execute the command and collect the corresponding output
 report d580_cmd
 var $fil = "/var/adm/extractor/honeycomb_extract"
 var $cmd = concat($cmd,' -o ',$fil)
 title '---+!! StorageTek 5800 Information'
 prefix
 {write '---+ Extractor Output'
  write '---## Using: ',encode($cmd)
 }
 call collectCommand({nam=>'st5800/extractor',\
   out=>{blk=>true,flt=>true,idx=>true,rpt=>${CUR.REPORT}},\
   err=>{blk=>true,flt=>true,hdr=>$ERR,rpt=>${CUR.REPORT}}\
   },$cmd)
 if isCreated(true)
 {write $TOP
  toc '3:[[',getFile(),'][rda_report][Extractor Execution Output]]'
 }

 # Collect the result file
 var $fil = concat($fil,'.tar.gz')
 report d580_fil
 prefix
 {write '---+ Extractor File'
  write '   * Links point to files that have been collected in their original \
              format. Opening them directly in your browser can present risks. \
              To prevent them, access the file outside the browser or use the \
              link to save them and use an adequate viewer.'
  write '|*File Path*| *Size*|*Last Modified Date*|'
 }
 if collectFile('st5800/extract/honeycomb_extract.tar.gz',$fil)
  write '|[[',last,'][_blank][',$fil,']] | ',getSize($fil),'|',\
         getLastModify($fil,'%d-%b-%Y&nbsp;%H:%M:%S'),' |'
 if isCreated(true)
 {write $TOP
  toc '3:[[',getFile(),'][rda_report][Extractor Result File]]'
 }

 # Adjust the table of content
 unpretoc
}

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
