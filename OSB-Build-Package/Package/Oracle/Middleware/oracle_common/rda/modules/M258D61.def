# M258D61.def: Collects StorEdge 6100 Series Disk Arrays Information
# $Id: M258D61.def,v 1.2 2012/08/17 14:55:20 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M258D61.def,v 1.2 2012/08/17 14:55:20 mschenke Exp $
#
# Change History
# 20120817  MSC  Improve index entries.

=head1 NAME

M258D61 - Collects StorEdge 6100 Series Disk Arrays Information

=head1 DESCRIPTION

This module collects information on StorEdge 6100 Series disk arrays.

=cut

use Mrc

# Initialization
var @COMMON_SECTIONS=('XPLR_all')
var $VALIDATE = true
keep $VALIDATE,@COMMON_SECTIONS

section begin

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
var $WRN = '* Links point to files that have been collected in their original \
              format. Opening them directly in your browser can present risks. \
              To prevent them, access the file outside the browser or use the \
              link to save them and use an adequate viewer.'

run XPLRlib('se61xx')

# -----------------------------------------------------------------------------
# XPLR_se61xx section
# -----------------------------------------------------------------------------

section XPLR_se61xx

# Validate the execution context
if !${XPLR_GLOBAL:true}
 return
call log_run('Processing D61 sections ...')

if $STADE = is_pkg_installed('SUNWstade')
 var ($bas,$ver) = (get_pkg_base('SUNWstade','SUNWstade','bin'),\
                    get_pkg_version('SUNWstade'))
elsif is_pkg_installed('SUNWsefms')
 var ($bas,$ver) = (get_pkg_base('SUNWsefms','SUNWsefms','bin'),'2.4')
else
 return log_info('SUNWstade and SUNWsefms packages are not installed to \
                  collect SE61XXSupportData')

=head2 d61 - StorEdge 6100 Series disk arrays information

Gathers the StorEdge 6100 Series disk arrays information using the following
commands:

=over 2

=item o C</opt/SUNWsefms/bin/ras_admin>

=item o C</opt/SUNWsefms/bin/*supportData>

=item o C</opt/SUNWstade/bin/ras_admin>

=item o C</opt/SUNWstade/bin/*SupportData>

=back

=cut

debug ' Inside D61 collection, gathering D61 information'
report d61
write '---+!! StorEdge 6100 Series Disk Arrays'
write $TOC
write '---+ Collection Overview'
call beginBlock(true)
call addBlock('E','D','disks/StorEdge/SE61XX/se61xx.log')

if !compare('valid',$ver,'2.4')
{write ${RDA.GMTIME},': Installed SUNWstade version ',$ver,\
       ' does not support SE61XX data collection.'
 write
}
elsif grepDir($bas,'^(s|[^\.].*s)upportdata','ifp')
{var $pgm = quote(last,'x')
 if !loadCommand(concat(catCommand($bas,'ras_admin'),\
                        ' device_detail 2>/dev/null'))
 {write ${RDA.GMTIME},': ',catFile($bas,'ras_admin'),\
        ' did not completed successfully.'
  write
 }
 elsif !grepLastFile('6130|6140','f')
 {write ${RDA.GMTIME},\
        ': No SE61XX array(s) installed or configured on this system'
  write
 }
 else
 {var ($lnk,%dev) = (true)
  loop $lin (grepLastFile('^\s*#','v'))
  {var $nam = lc(trim(field(':',0,$lin)))
   var $val = trim(field(':',1,$lin))
   if match($nam,'^(devicename|device name)$')
    var $dev{'nam'} = $val
   elsif compare('eq',$nam,'typ')
   {if match($val,'61[34]0')
     var $dev{'typ'} = $val
   }
   elsif compare('eq',$nam,'ip address')
    var $dev{'adr'} = $val
   elsif compare('eq',$nam,'key')
    var $dev{'key'} = $val
   elsif length($nam)
    next
   elsif and(exists($dev{'nam'}),exists($dev{'typ'}),exists($dev{'key'}))
   {var $box = cleanBox()
    if $STADE
     $cmd = join(' ',$pgm,$dev{'key'},quote($box,'x'),\
                     'extract','2>/dev/null')
    else
     $cmd = join(' ',$pgm,'-d',$dev{'key'},'-p',quote($box,'x'),'-o',\
                     'extract','2>/dev/null')
    call command($cmd)
    if getSize(catFile($box,'extract.zip'))
    {var ($lnk{$dev{'nam'}}) = collectData(\
       concat('disks/StorEdge/ST61XX/',$dev{'nam'},'/',\
              $dev{'nam'},'_xtract.zip'),\
       lastFile(),['C','supportdata (extract.zip)'])

     write ${RDA.GMTIME},': ST61XX Explorer data for ',$dev{'nam'},\
           ' was collected successfully.'
     write
     if and($lnk,match($dev{'typ'},'6130'))
     {call addEntry('E','L','disks/StorEdge/SE6130','SE61XX')
      $lnk = false
     }
    }
    var %dev = ()
   }
  }
  var $box = cleanBox()
 }
}
else
{write ${RDA.GMTIME},': ',catFile($bas,'/supportData'),' not installed'
 write
}
call endBlock()
write $TOP

# Include device files
write '---+ Supportdata Results'
write $WRN
write '|*Device*|'
loop $dev (keys(%lnk))
 write '|[[',$lnk{$dev},'][_blank][',$dev,']]|'
write $TOP
toc '2:[[',getFile(),'][rda_report][StorEdge 6100 Series Disk Arrays]]'

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
