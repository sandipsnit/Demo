# M262LDAP.def: Collects LDAP Information
# $Id: M262LDAP.def,v 1.10 2012/06/05 08:50:11 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M262LDAP.def,v 1.10 2012/06/05 08:50:11 mschenke Exp $
#
# Change History
# 20120605  JGS  Extend the do_remote macro.

=head1 NAME

M262LDAP - Collects LDAP Information

=head1 DESCRIPTION

This module collects LDAP-related information.

=cut

use Mrc

# Initialization
var $VALIDATE = true
keep $VALIDATE

section begin

var $ERR = '---## Associated Errors'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

run XPLRlib('ldap')

#------------------------------------------------------------------------------
# XPLR_ldap section
#------------------------------------------------------------------------------

section XPLR_ldap

# Validate the execution context
call log_run('Processing LDAP sections ...')

=head2 LDAP-Related Configuration

Gathers LDAP information from the specified zones using the following commands:

=over 2

=item o C</usr/bin/ldaplist>

=item o C</usr/bin/ldaplist -d>

=item o C</usr/bin/ldaplist -l>

=item o C</usr/bin/ldaplist -l profile>

=item o C</usr/lib/ldap/ldap_cachemgr -g>

=item o C</usr/bin/ldapsearch -h ${SERVER} -b ${BASEDN} aci=\* aci>

=item o C</usr/bin/ldapsearch -h ${SERVER} -b cn=monitor -s base objectclass=*>

=item o C</usr/bin/ldapsearch -h ${SERVER} -b cn=monitor -s one objectclass=*>

=item o C</usr/sbin/ldapclient -l>

=item o C</usr/sbin/ldapclient list>

=item o C</usr/sbin/directoryserver -s ${INSTANCE} monitor>

=back

Also collects the following configuration files:

=over 2

=item o F</var/ldap/*>

=item o F</var/ds5/$DIRECTORY/logs/access>

=item o F</var/ds5/$DIRECTORY/logs/errors>

=back

=cut

debug ' Inside LDAP collection, gathering LDAP information'
pretoc '2:LDAP'

# Populate table of commands to collect => [zone,outfile,command,descr,grouphdr]
loop $rec (get_zones(true))
{if $loc = defined($nam = $rec->[0])
  var ($ttl,$pre,$exe) = (concat('From Zone ',$nam),\
                          concat('zones/',$nam),\
                          concat('/usr/sbin/zlogin ',$nam))
 else
  var ($ttl,$pre,$exe) = ('From Global Zone')
 var $top = $rec->[1]

 var (@cmd,@fil) = ()
 if compare('FINAL',$rec->[2],'5.7')
  call push(@cmd,\
    ['var/ldap/not-ldap.out',\
     undef,\
     'LDAP data gathered for Solaris 5.8 or higher versions only.',\
     '---+ LDAP'])
 else
 {# Treat a LDAP client
  if testFile('f',catFile($top,'var','ldap','ldap_client_file'))
  {call push(@cmd,\
     '---+ LDAP Client',\
     ['var/ldap/client/ldaplist',\
      '/usr/bin/ldaplist',undef,\
      '---++ Naming Information'],\
     ['var/ldap/client/ldaplist-d',\
      '/usr/bin/ldaplist','-d',\
      '---++ Database Attributes'],\
     ['var/ldap/client/ldaplist-l',\
      '/usr/bin/ldaplist','-l',\
      '---++ All Attributes'],\
     ['var/ldap/client/ldaplist-l.-profile',\
      '/usr/bin/ldaplist','-l profile',\
      '---++ Profile Attributes'],\
     ['var/ldap/client/ldap_cachemgr-g',\
      '/usr/lib/ldap/ldap_cachemgr','-g',\
      '---++ Cache Manager Configuration and Statistics'])
   var $bdn = value(grepFile('/var/ldap/ldap_client_file',\
                             '^NS_LDAP_SEARCH_BASEDN=','f'))
   if value(grepFile('/var/ldap/ldap_client_file','^NS_LDAP_SERVERS=','f'))
   {loop $svr (split('\s*,\s*',last))
    {var $cmd = concat('/usr/bin/ldapsearch -h ',$svr,' -b ')
     call push(@cmd,\
       [concat('var/ldap/client/acidefs.',$svr),\
        $cmd,concat($bdn,' aci=\* aci'),\
        '---++ Access Control Instructions (ACI)'],\
       [concat('var/ldap/client/statistics_base.',$svr),\
        $cmd,'cn=monitor -s base objectclass=*',\
        '---++ Statistics (Scope Base)'],\
       [concat('var/ldap/client/statistics_one.',$svr),\
        $cmd,'cn=monitor -s one objectclass=*',\
        '---++ Statistics (Scope One Level)'])
    }
   }
   if compare('SAME',$rec->[2],'5.8')
    call push(@cmd,\
      ['var/ldap/client/ldapclient-l',\
       '/usr/sbin/ldapclient','-l',\
       '---++ LDAP Client Cache'])
   else
    call push(@cmd,\
      ['var/ldap/client/ldapclient-list',\
       '/usr/sbin/ldapclient','list',\
       '---++ LDAP Client Cache'])

   # Indicate the LDAP client directory structure to collect
   call push(@fil,\
     ['var/ldap/client','/var/ldap',true])
  }
  else
   call push(@cmd,\
     ['var/ldap/client/not-configured.out',\
      undef,\
      'This system is not configured as a native LDAP client',\
      '---+ LDAP Client'])

  # Treat a LDAP server
  if compare('SAME',$rec->[2],'5.8')
   call push(@cmd,\
     ['var/ldap/server/no_bundled_srv.out',\
      undef,\
      'There is no LDAP-server software bundled with this Solaris version',\
      '---+ LDAP Server'])
  elsif testDir('d',catDir($top,'var','ds5'))
  {loop $dir (findDir(lastDir(),'^\.','v'))
   {var $ins = replace($dir,'slapd-')
    call push(@cmd,\
      concat('---+ LDAP Server ',$ins),\
      [concat('var/ldap/server/',$ins,'/directoryserver-s-monitor'),\
       '/usr/sbin/directoryserver',concat('-s ',$ins,' monitor'),\
       '---++ Performance Monitoring Information'])

    # Indicate the LDAP server files to collect
    call push(@fil,\
      [concat('var/ldap/server/',$ins,'/access'),\
       concat('/var/ds5/',$dir,'/logs/access')],\
      [concat('var/ldap/server/',$ins,'/errors'),\
       concat('/var/ds5/',$dir,'/logs/errors')])
   }
  }
  else
   call push(@cmd,\
     ['var/ldap/server/not-configured.out',\
      undef,'This system is not configured as a LDAP server',\
      '---+ LDAP Server'])
 }

 # Create the report
 debug ' Inside LDAP collection, collecting from ',nvl($nam,'global'),' zone'
 report concat('ldap_z_',nvl($nam,'global'))
 title '---+!! ',$ttl
 title $TOC

 # Collect the commands
 call do_remote($pre,$exe,$top,@cmd)

 # Collect the files
 prefix
 {title '---+ Configuration files'
  title '   * Links point to files that have been collected in their original \
              format. Opening them directly in your browser can present \
              risks. To prevent them, access the file outside the browser or \
              use the link to save them and use an adequate viewer.'
  write '|*File Path*| *Size*|*Last Modified Date*|'
 }
 call do_collect_fil(@fil)
 if hasOutput(true)
  write $TOP

 # Add the file to the table of content
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][',$ttl,']]'
}
unpretoc

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
