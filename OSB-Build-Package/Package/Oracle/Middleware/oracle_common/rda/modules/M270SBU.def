# M270SBU.def: Collects Solstice Backup Information
# $Id: M270SBU.def,v 1.3 2012/04/02 07:29:18 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/M270SBU.def,v 1.3 2012/04/02 07:29:18 mschenke Exp $
#
# Change History
# 20120402  MSC  Extend the collection macros.

=head1 NAME

M270SBU - Collects Solstice Backup Information

=head1 DESCRIPTION

This module collects Solstice Backup information.

=cut

use Mrc

# Initialization
var $VALIDATE = true
keep $VALIDATE

section begin

var $ERR = '---## Associated Errors'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

run XPLRlib('sbu')

#------------------------------------------------------------------------------
# XPLR_sbu section
#------------------------------------------------------------------------------

section XPLR_sbu

# Validate the execution context
if !${XPLR_GLOBAL:true}
 return
call log_run('Processing SBU sections ...')
if !testDir('d','/usr/sbin/nsr')
 return log_info('Solstice Backup not installed')

pretoc '2:Solstice Backup'

=head2 sbu_cmd - Commands

Gathers the Solstice Backup information using the following commands:

=over 2

=item o C</usr/sbin/nsr/mminfo -av>

=item o C</usr/sbin/nsr/mminfo -aV>

=item o C</usr/sbin/nsr/nsrls>

=item o C</bin/ls -alF /nsr/index>

=item o C</bin/ls -alLF /nsr/index>

=item o C</bin/ls -alF /usr/sbin/nsr>

=item o C</bin/ls -alF /usr/bin/nsr>

=item o C</bin/ls -alF /dev/rmt>

=item o C</bin/ls -alLF /dev/rmt>

=back

=cut

debug ' Inside SBU collection, gathering command outputs'
report sbu_cmd
title '---+!! Solstice Backup Commands'
title $TOC
call do_exec(\
  ['sbu/rmt/devices',\
   '/bin/ls','-alF /dev/rmt',\
   '---+ Content of ``/dev/rmt`` Directory'],\
  ['sbu/rmt/devices_L',\
   '/bin/ls','-alLF /dev/rmt',\
   '---+ Content of ``/dev/rmt`` Directory (Dereferenced)'],\
  ['sbu/index/clients_l',\
   '/bin/ls','-alF /nsr/index',\
   '---+ Content of NetWorker Clients Indexes Directory'],\
  ['sbu/index/clients_L',\
   '/bin/ls','-alLF /nsr/index',\
   '---+ Content of NetWorker Clients Indexes Directory (Dereferenced)'],\
  ['sbu/sbin/nsr',\
   '/bin/ls','-alF /usr/sbin/nsr',\
   '---+ Content of NetWorker Directory'],\
  ['sbu/bin/nsr',\
   '/bin/ls','-alF /usr/bin/nsr',\
   '---+ Content of NetWorker Directory'],\
  ['sbu/nsrls',\
   '/usr/sbin/nsr/nsrls',undef,\
   '---+ NetWorker Index Files Statistics'],\
  ['sbu/mminfo-av',\
   '/usr/sbin/nsr/mminfo','-av',\
   '---+ NetWorker Savesets Report'],\
  ['sbu/mminfo-aV',\
   '/usr/sbin/nsr/mminfo','-aV',\
   '---+ NetWorker Savesets Report'])
if isCreated(true)
 toc '3:[[',getFile(),'][rda_report][Commands]]'

=head2 sbu_files - Files

Gathers the following files:

=over 2

=item o F</nsr/logs/daemon.log>

=item o F</nsr/logs/daemon.raw>

=item o F</nsr/logs/messages>

=item o F</nsr/logs/summary>

=item o F</nsr/res/*>

=back

=cut

debug ' Inside SBU collection, collecting key files'
report sbu_files
prefix
{write '---+!! Solstice Backup Files'
 write '   * Links point to files that have been collected in their original \
             format. Opening them directly in your browser can present risks. \
             To prevent them, access the file outside the browser or use the \
             link to save them and use an adequate viewer.'
 write '|*File Path*| *Size*|*Last Modified Date*|'
}
if ?get_pkg_version('SUNWebsc')
{if compare('VALID',last,'7.4')
  call do_collect_fil(['sbu/logs/daemon.raw','/nsr/logs/daemon.raw'])
 else
  call do_collect_fil(['sbu/logs/daemon.log','/nsr/logs/daemon.log'])
}
call do_collect_fil(\
  ['sbu/logs/messages','/nsr/logs/messages'],\
  ['sbu/logs/summary', '/nsr/logs/summary'])
call do_collect_dir('sbu/nsr','/nsr/res',true)
if isCreated(true)
{write $TOP
 toc '3:[[',getFile(),'][rda_report][Key Files]]'
}

unpretoc

=head1 SEE ALSO

L<S150XPLR.def|modules::S150XPLR>,
L<XPLRlib.def|modules::XPLRlib>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
