# OVDr11.def: Defines Common Macros for Oracle Virtual Directory 11g
# $Id: OVDr11.def,v 2.6 2012/01/03 13:34:43 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/OVDr11.def,v 2.6 2012/01/03 13:34:43 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

OVDr11 - Defines Common Macros for Oracle Virtual Directory 11g

=head1 DESCRIPTION

This persistent submodule regroups macros that are common to Oracle Virtual
Directory in Oracle Fusion Middleware 11g.

The following macros are available:

=cut

# Make the module persistent and share macros
keep $KEEP_BLOCK,@SHARE_MACROS
var @SHARE_MACROS = ('collect_ovd_generic','collect_ovd_instance')

# Load the common macros
run library()

=head2 S<collect_ovd_generic($hom)>

This macro gathers Oracle Virtual Directory list of executables

=cut

macro collect_ovd_generic
{var ($hom) = @arg

 debug ' Inside OVD module, gathering list of executables'
 report ovd_bin
 var $dir = catDir($hom,'bin')
 prefix
  write '---+ List of Files in ',encode($dir)
 call statDir('an',$dir)
 if isCreated(true)
  toc '2:[[',getFile(),'][rda_report][Executable List]]'
}

=head2 S<collect_ovd_instance($abr,$top,\%cmp)>

This macro collects Oracle Virtual Directory-related configuration and log
files.

=cut

macro collect_ovd_instance
{var ($abr,$top,\%cmp) = @arg
 import $TAIL

 debug ' - Getting Oracle Virtual Directory configuration and log files'
 loop $key (keys(%cmp))
 {var ($typ,$cmp) = split('\|',$key,2)
  next !match($typ,'^OVD$',true)
  debug '   - ',$cmp,' component'
  call setAbbr(concat($abr,'_c_',$cmp))
  pretoc "2:'",$cmp,"' OVD Component"

  # Collect the config files
  pretoc '3:Configuration Files'
  call sort_files(4,$TAIL,\
                    grepDir(catDir($top,'config',$typ,$cmp),'^\.+$','drv'))
  unpretoc

  # Collect the log files
  pretoc '3:Log Files'
  call sort_files(4,$TAIL,\
   grepDir(catDir($top,'diagnostics','logs',$typ,$cmp),'\.log','inp'))
  unpretoc 2
 }
 call setAbbr($abr)
}

=head1 SEE ALSO

L<library.def|modules::library>

=begin credits

=over 10

=item RDA 4.15: Greg Cook, Andrew Salt.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
