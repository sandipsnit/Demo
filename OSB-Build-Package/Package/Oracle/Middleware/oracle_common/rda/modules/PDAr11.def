# PDAr11.def: Defines Common Macros for Portal 11g
# $Id: PDAr11.def,v 2.6 2012/01/03 13:34:43 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/PDAr11.def,v 2.6 2012/01/03 13:34:43 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

PDAr11 - Defines Common Macros for Portal 11g

=head1 DESCRIPTION

This persistent submodule regroups macros that are common to Portal in Oracle
Fusion Middleware 11g.

The following macro is available:

=cut

# Make the module persistent and share macros
keep $KEEP_BLOCK,@SHARE_MACROS
var @SHARE_MACROS = ('analyze_ohs_logs')

# Load the common macros
run IAS()

=head2 S<analyze_ohs_logs($abr,$top,$use,$ver,\%cmp)>

This macro collects Portal instance information.

=cut

macro analyze_ohs_logs
{var ($abr,$top,$use,$ver,\%cmp) = @arg
 import %HTTPDCONF,%HTTPDERRLOG

 debug ' - Getting Portal performance related information'
 loop $key (keys(%cmp))
 {var ($typ,$cmp) = split('\|',$key,2)
  next !match($typ,'^OHS$',true)
  debug '   - ',$cmp,' component'
  var (%HTTPDCONF,%HTTPDERRLOG) = ()
  var ($fil) = (grepDir(catDir($top,'config',$typ,$cmp),\
                '^httpd(s)?\.conf$','pf'))
  call setTempSetting('HTTPD_CONF_LOCATION',$fil)

  # Get the ohs error file (more recent complete one in case of rotation)
  call httpServer_getListenerConf(true,$top,$typ,$cmp)
  call httpServer_getListenerLogs(true,$top,$typ,$cmp)
  var ($max,$fil) = (0)
  loop $log (keys(%HTTPDERRLOG))
  {if expr('>',$HTTPDERRLOG{$log},$max)
    var ($fil,$max) = ($log,$HTTPDERRLOG{$log})
  }

  # Analyze the performances
  if $fil
  {pretoc "2:'",$cmp,"' OHS Component"
   call setAbbr(concat($abr,'_c_',$cmp))
   run PDAperf(3,$use,$ver,$fil)
   unpretoc
  }
 }
 call setTempSetting('HTTPD_CONF_LOCATION')
 call setAbbr($abr)
}

=head1 SEE ALSO

L<PDAperf.def|modules::PDAperf>

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
