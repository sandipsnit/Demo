# S090OCM.def: Setting up Configuration Manager Interface
# $Id: S090OCM.def,v 2.24 2012/01/03 13:34:43 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S090OCM.def,v 2.24 2012/01/03 13:34:43 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S090OCM - Interacts with the Oracle Configuration Manager

=head1 DESCRIPTION

This module executes Oracle Configuration Manager to collect configuration
data when possible.

=cut

# Check if the collection must be done
if getSetting('NO_OCM')
 return

macro skip_ocm
{if !getSetting('RDA_ARGC')
  return false
 else
 {loop $mod (split('\|',getSetting('RDA_ARGV')))
  {if match($mod,'^((T[:/])?(S090)?OCM)$',true)
    return false
  }
 }
 output E,status
 var $fil = getFile('/')
 close
 return isNewer($fil,1)
}

if skip_ocm()
 return

# Initialization
var $INSTANCE_ROOT  = getSetting('INSTANCE_ROOT')
var $OCM_ANNOTATION = getSetting('OCM_ANNOTATION')
var $OCM_INSTANCES  = getSetting('OCM_INSTANCES','N')
var $ORACLE_HOME    = getSetting('ORACLE_HOME')
var $ORACLE_PARENT  = getSetting('ORACLE_PARENT',catDir($ORACLE_HOME,upDir()))

var @ERR
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

echo tput('bold'),'Processing OCM module ...',tput('off')

# Load the common macros
run OCMlib()

# Create the reports
var $toc = $[OUT]->add_report('e','index',0)
title {$toc} '2:Oracle Configuration Manager'
var $rps = $[OUT]->add_report('e','status',0)
title {$rps} '---+!! Oracle Configuration Manager Status'
title {$rps} $TOC
var $rpc = $[OUT]->add_report('e','collect',0)
title {$rpc} '---+!! Configuration Collection'
title {$rpc} $TOC

# Collect the configuration when possible
if $INSTANCE_ROOT
{# Save the environment variable
 var $env = setEnv('ORACLE_CONFIG_HOME')

 # Collect OCM in the Oracle home
 if !or(sameDir($ORACLE_HOME,catDir($ORACLE_PARENT,'utils')),\
        sameDir($ORACLE_HOME,catDir($ORACLE_PARENT,'oracle_common')))
 {if nvl(get_ocm_home(catDir($ORACLE_HOME,'ccr'),'Oracle Home'),\
         get_ocm_home(catDir($ORACLE_HOME,'livelink'),'Oracle Home'))
  {var ($ccr,$cmd,$ttl) = last

   if get_ocm_config($ccr)
   {var $cfg = last

    if match($OCM_ANNOTATION,'^\w+$')
    {debug ' Inside OCM module, collecting the Oracle Home configuration \
             (can take time)'
     call do_status($toc,$rps,$rpc,$cmd,$cfg,$OCM_ANNOTATION,$ttl)
     call setSetting('LAST_INFO_S090OCM')
    }
    else
    {var ($flg,$not) = get_ocm_mode($cfg,'oracle_home_config')

     debug ' Inside OCM module, collecting the Oracle Home configuration \
             (can take time)'
     if do_collect($toc,$rps,$rpc,$cmd,$cfg,$not,$flg,$ttl)
     {debug ' Inside OCM module, transfering the OH configuration'
      call transfer(catDir($cfg,'state','upload'),'ocmconfig.jar',\
             ${OUT.E},concat(${CUR.PREFIX},'payload_oh.jar'),\
             true)
     }
     call setSetting('LAST_INFO_S090OCM')
    }
   }
   else
    call push(@ERR,'OCM-09102: No OCM configuration found (Oracle home)')
  }
 }

 # Collect OCM in the Middleware home
 if ?nvl(get_ocm_home(catDir($ORACLE_PARENT,'oracle_common','ccr'),\
                      'Middleware Home'),\
         get_ocm_home(catDir($ORACLE_PARENT,'utils','ccr'),\
                      'Middleware Home'))
 {var ($ccr,$cmd,$ttl) = last

  if get_ocm_config($ccr)
  {var ($flg,$not) = get_ocm_mode($cfg = last,'livelink_config')

   # Adapt the environment
   var ($bea,$wls) = get_wls_config($ORACLE_PARENT)
   var $bea = setEnv('BEA_HOME',$bea)
   var $wls = setEnv('WL_HOME', $wls)

   # Perform the collection
   debug ' Inside OCM module, collecting the Middleware Home configuration \
           (can take time)'
   if do_collect($toc,$rps,$rpc,$cmd,$cfg,$not,$flg,$ttl)
   {debug ' Inside OCM module, transfering the OCM results'
    call transfer(catDir($cfg,'state','upload'),'ocmconfig.jar',\
           ${OUT.E},concat(${CUR.PREFIX},'payload_mw.jar'),\
           true)
   }
   call setSetting('LAST_INFO_S090OCM')

   # Restore the environment
   call setEnv('BEA_HOME',$bea)
   call setEnv('WL_HOME', $wls)
  }
  else
   call push(@ERR,'OCM-09102: No OCM configuration found (Middleware)')
 }

 # Collect OCM in the instance homes
 var $cnt = 0
 if compare('eq',$OCM_INSTANCES,'A')
  var @dir = get_selected_instances(get_all_instances())
 elsif compare('eq',$OCM_INSTANCES,'S')
  var @dir = get_selected_instances()
 else
  var @dir = ()
 loop $dir (@dir)
 {var $ins = concat('ins',incr($cnt))
  var ($flg,$not) = get_ocm_mode($cfg = catDir($dir,'ccr'),'livelink_config')

  debug ' Inside OCM module, collecting OCM in ',$dir,' (can take time)'
  call setEnv('ORACLE_CONFIG_HOME',$dir)
  next !grepFile(catFile($dir,'ccr','config','collector.properties'),\
                 '^ccr\.binHome=','f')
  var $ccr = value(last)
  if or(isWindows(),isCygwin())
   var $ccr = s($ccr,'^(\w+)\\:','$1:')
  if do_collect($toc,$rps,$rpc,catCommand($ccr,'bin','emCCR'),\
                $cfg,$not,$flg,concat('From Instance Home ',$dir))
  {debug ' Inside OCM module, transfering the OCM results'
   call transfer(catDir($cfg,'state','upload'),'ocmconfig.jar',\
          ${OUT.E},concat(${CUR.PREFIX},concat('payload_',$ins,'.jar')),\
          true)
  }
  call setSetting('LAST_INFO_S090OCM')
 }

 # Restore the environment
 call setEnv('ORACLE_CONFIG_HOME',$env)
}
elsif ?nvl(get_ocm_home(catDir($ORACLE_HOME,'ccr'),'Oracle Home'),\
           get_ocm_home(catDir($ORACLE_HOME,'livelink'),'Oracle Home'),\
           get_ocm_home(catDir($ORACLE_PARENT,'oracle_common','ccr'),\
                        'Middleware Home'),\
           get_ocm_home(catDir($ORACLE_PARENT,'utils','ccr'),\
                        'Middleware Home'),\
           get_ocm_home(catDir(${WLS_HOME:''},upDir(),'oracle_common','ccr'),\
                        'Middleware Home'),\
           get_ocm_home(catDir(${WLS_HOME:''},upDir(),'utils','ccr'),\
                        'Middleware Home'))
{var ($ccr,$cmd,$ttl) = last

 call setSetting('LAST_INFO_S090OCM')
 if get_ocm_config($ccr)
 {var $cfg = last
 
  if match($OCM_ANNOTATION,'^\w+$')
  {debug ' Inside OCM module, collecting the ',$ttl,' configuration \
           (can take time)'
   call do_status($toc,$rps,$rpc,$cmd,$cfg,$OCM_ANNOTATION)
  }
  else
  {var ($flg,$not) = get_ocm_mode($cfg,'oracle_home_config')

   debug ' Inside OCM module, collecting the ',$ttl,' configuration \
           (can take time)'
   if do_collect($toc,$rps,$rpc,$cmd,$cfg,$not,$flg,$ttl)
   {debug ' Inside OCM module, transfering the OCM results'
    call transfer(catDir($cfg,'state','upload'),'ocmconfig.jar',\
           ${OUT.E},concat(${CUR.PREFIX},'payload.jar'),\
           true)
   }
  }
 }
 else
  call push(@ERR,'OCM-09102: No OCM configuration found')
}

# Treat other OCM requests
var %req = ()
loop $key (grepSetting('_OCM_REQUEST$','n'))
{var $dir = catDir(getSetting($key))
 if missing($req{$dir})
  var $req{$dir} = getSetting(replace($key,'REQUEST$','TITLE'),$key)
}
loop $dir (keys(%req,'SA'))
{if get_ocm_home($dir,$req{$dir})
 {var ($ccr,$cmd,$ttl) = last

  if get_ocm_config($ccr)
  {var $cfg = last
   var ($flg,$not) = get_ocm_mode($cfg,'livelink_config')

   debug ' Inside OCM module, collecting the ',$ttl,' configuration \
           (can take time)'
   if do_collect($toc,$rps,$rpc,$cmd,$cfg,$not,$flg,$ttl)
   {debug ' Inside OCM module, transfering the OCM results'
    call transfer(catDir($cfg,'state','upload'),'ocmconfig.jar',\
           ${OUT.E},concat(${CUR.PREFIX},'payload.jar'),\
           true)
   }
  }
 }
}

# Report the errors
if @ERR
 call setSetting('LAST_INFO_S090OCM',join('%BR%',@ERR),'T','Execution error')

# Render the reports
if $rpc->is_created(true)
 call $rpc->render
if $rps->is_created(true)
 call $rps->render

=head1 SEE ALSO

L<OCMlib.def|modules::OCMlib>

=begin credits

=over 10

=item RDA 4.20: Wes Root.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
