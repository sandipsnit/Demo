# S316GFS.def: Collects Oracle GlassFish Server Information
# $Id: S316GFS.def,v 1.5 2012/01/03 13:34:45 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S316GFS.def,v 1.5 2012/01/03 13:34:45 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S316GFS - Collects Oracle GlassFish Server Information

=head1 DESCRIPTION

This module collects Oracle GlassFish Server-related information. The module
covers Oracle GlassFish Server Release 2.0 and later.

The following reports can be generated and are regrouped under
C<GlassFish Server>:

=cut

echo tput('bold'),'Processing GFS module ...',tput('off')

# Initialization
var $DAS_HOME = getSetting('GFS_DAS_HOME','')
var $GFS_HOME = getSetting('GFS_HOME','')
var $INS_HOME = getSetting('GFS_INSTANCE_HOME','')
var $TAIL     = getSetting('RDA_TAIL',1000)

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '1:GlassFish Server'

# Load the common macros
run library()

=head1 GLASSFISH SERVER INFORMATION

=head2 version_info - Version Information

Gathers Oracle GlassFish Server version information.

=cut

debug ' Inside GFS module, gathering version information'
pretoc '2:Server Information'
if testFile('fx',catFile($GFS_HOME,'bin',${AS.EXE:'asadmin'}))
{report version_info
 var $cmd = concat(lastCommand(),' version --verbose --local')
 prefix
 {write '---+ Oracle GlassFish Server Version Information'
  write '---## Using: ',encode($cmd)
 }
 if !writeCommand($cmd)
 {var $cmd = concat(lastCommand(),' version --verbose')
  call writeCommand($cmd)
 }
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Version Information]]'
}

=head2 Configuration Files

Gathers Oracle GlassFish Server-related configuration files.

=cut

if testDir('d',$GFS_HOME)
{debug ' Inside GFS module, collecting the GlassFish configuration files'
 pretoc '3:Configuration Files'
 call sort_files(4,0,\
  grepDir(catDir($GFS_HOME,'glassfish','config'),'^\.+$','pv'))
 unpretoc
}
unpretoc

=head1 DOMAIN ADMINISTRATION SERVER INFORMATION

=head2 Configuration Files

Gathers Domain Administration Server (DAS)-related configuration files.

=cut

if testDir('d',$DAS_HOME)
{debug ' Inside GFS module, collecting the DAS configuration files'
 pretoc '2:Domain Server Information'
 pretoc '3:Configuration Files'
 call sort_files(4,0,grepDir(catDir($DAS_HOME,'config'),'^\.+$','pv'))
 unpretoc

=head2 Log Files

Gathers Domain Administration Server (DAS)-related log files.

=cut

 debug ' Inside GFS module, collecting the DAS log files'
 pretoc '3:Log Files'
 call sort_files(4,$TAIL,grepDir(catDir($DAS_HOME,'logs'),'^\.+$','pv'))
 unpretoc 2
}

=head1 INSTANCE INFORMATION

=head2 Configuration Files

Gathers instance-related configuration files.

=cut

if testDir('d',$INS_HOME)
{debug ' Inside GFS module, collecting the instance configuration files'
 pretoc '2:Instance Information'
 pretoc '3:Configuration Files'
 call sort_files(4,0,grepDir(catDir($INS_HOME,'config'),'^\.+$','pv'))
 unpretoc

=head2 Log Files

Gathers instance-related log files.

=cut

 debug ' Inside GFS module, collecting the instance log files'
 pretoc '3:Log Files'
 call sort_files(4,$TAIL,grepDir(catDir($INS_HOME,'logs'),'^\.+$','pv'))
 unpretoc 2
}

unpretoc

=head1 SEE ALSO

L<library.def|modules::library>

=begin credits

=over 10

=item RDA 4.26: Jeff Brown.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
