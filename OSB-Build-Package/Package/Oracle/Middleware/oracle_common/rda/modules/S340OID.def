# S340OID.def: Collects Oracle Internet Directory Information
# $Id: S340OID.def,v 2.15 2012/01/03 13:34:45 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S340OID.def,v 2.15 2012/01/03 13:34:45 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S340OID - Collects Oracle Internet Directory Information

=head1 DESCRIPTION

This module collects Oracle Internet Directory-related information.

The following reports can be generated and are regrouped under C<Oracle
Internet Directory>:

=cut

echo tput('bold'),'Processing Oracle Internet Directory module ...',tput('off')

# Initialization
var $ORACLE_HOME = getSetting('ORACLE_HOME','')
var $TAIL        = getSetting('RDA_TAIL',1000)

var $LDAP_HOME   = catDir($ORACLE_HOME,'ldap')

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

# Collect the Oracle Internet Directory information
if !?getSetting('OID_PRIMARY')
{# Collect the generic information
 pretoc '1:Oracle Internet Directory'
 run OIDinfo(false,false)
 
 # Load the common macros
 run library()

 # Collect log files related to Oracle Internet Directory
 var $dir = catDir($LDAP_HOME,'log')

=head2 oidmon.log

Displays the last lines of the monitor log.

=cut

 debug ' Inside OID module, tail the monitor log'
 call tail_report($dir,'oidmon.log',$TAIL)

=head2 oidstack.log

Displays the last lines of the stack logs.

=cut

 debug ' Inside OID module, tail the stack logs'
 loop $nam (grepDir($dir,'^oidstack.*\.log$','in'))
  call tail_report($dir,$nam,$TAIL)

=head2 oidldapd.log

Displays the last lines of the LDAP logs.

=cut

 debug ' Inside OID module, tail the LDAP logs'
 loop $nam (grepDir($dir,'^oidldapd.*\.log$','in'))
  call tail_report($dir,$nam,$TAIL)

=head2 oidrepld.log

Displays the last lines of the replication logs.

=cut

 debug ' Inside OID module, tail the replication logs'
 loop $nam (grepDir($dir,'^oidrepld.*\.log$','in'))
  call tail_report($dir,$nam,$TAIL)

=head2 odisrv.log

Displays the last lines of the server logs.

=cut

 debug ' Inside OID module, tail the server logs'
 loop $nam (grepDir($dir,'^odisrv.*\.log$','in'))
  call tail_report($dir,$nam,$TAIL)

=head2 $OH/ldap/odi/log

Displays the contents of the files in the F<$ORACLE_HOME/ldap/odi/log>
directory.

=cut

 debug ' Inside OID module, listing the files in $OH/ldap/odi/log directory'
 call sort_files(3,$TAIL,grepDir(catDir($LDAP_HOME,'odi','log'),'.','p'))

=head2 $OH/ldap/load

Displays the contents of the files in the F<$ORACLE_HOME/ldap/load> directory.

=cut

 debug ' Inside OID module, listing the files in $OH/ldap/load directory'
 call sort_files(3,$TAIL,grepDir(catDir($LDAP_HOME,'load'),'.','p'))

=head2 $OH/ldap/das

Displays the contents of the files in the F<$ORACLE_HOME/ldap/das> directory
except for the F<oiddas.ear> file.

=cut

 debug ' Inside OID module, listing the files in $OH/ldap/das directory'
 call sort_files(3,$TAIL,\
                 grepDir(catDir($LDAP_HOME,'das'),'^oiddas\.ear$','vip'))
 unpretoc

=head2 not_found - Not found

Used as a warning when none of the Oracle Internet Directory files are found.

=cut

 if !isTocCreated(true)
 {debug ' Inside OID module, no file found'
  report not_found
  write 'No Oracle Internet Directory configuration files were found and all \
         files were optional. This does not mean there was necessarily a \
         problem with RDA, but it should be investigated.'
  toc $MNU
  toc '2:[[',getFile(),'][rda_report][Not found]]'
 }
}

=head1 IDENTITY MANAGEMENT INFORMATION FOR ORACLE FUSION MIDDLEWARE

Includes the reports generated by the IREQ module about the Oracle instances
and their associated Oracle homes.

=cut

elsif or(getSetting('OID_REQ_IREQ'),getSetting('OID_REQ_WREQ'))
{# Collect the OID 11g information
 toc '%PUSH("0:   * Oracle Internet Directory")%'
 toc '%PUSH("1+:Common Product Home")%'
 toc '%INCLUDE("',${CUR.GROUP},'_IREQ_OID_CH.toc")%'
 toc '%POP%'
 loop $key (grepSetting('^OID_OI\d+_REQ_INSTANCE$'))
 {var $tid = substr($key,0,-13)
  var $ins = basename(getSetting($key,''))
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("0:      * ',"'",$ins,"'",' Instance")%'
  toc '%PUSH("1++:Associated Oracle Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',replace($tid,'_OI','_OH'),'.toc",1)%'
  toc '%POP%'
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("1++:Instance Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',$tid,'.toc")%'
  toc '%POP4%'
 }
 toc '%PUSH("%SPLIT%")%'
 toc '%PUSH("1+:Oracle WebLogic Server Overview")%'
 toc '%INCLUDE("',${CUR.GROUP},'_WREQ_OID_TOP.toc")%'
 toc '%POP2%'
 toc '%PUSH("%SPLIT%")%'
 toc '%PUSH("1+:Oracle WebLogic Server Domain")%'
 toc '%INCLUDE("',${CUR.GROUP},'_WREQ_OID_DOM.toc")%'
 toc '%POP3%'
}

=head1 SEE ALSO

L<S301WREQ.def|modules::S301WREQ>,
L<S313IREQ.def|modules::S313IREQ>,
L<library.def|modules::library>,
L<OIDinfo.def|modules::OIDinfo>,
L<OIDr11.def|modules::OIDr11>

=begin credits

=over 10

=item RDA 4.4:  Wes Root.

=item RDA 4.9:  Irina Donaldson, Anita Hegde, Satheesan Vettahil.

=item RDA 4.12: Raju Channabasappa, Wes Root, Jerry Smith, Satheesan Vettahil.

=item RDA 4.15: Greg Cook, Andrew Salt, Olaf Stullich.

=item RDA 4.20: Hector Viveros.

=item RDA 4.21: Hector Viveros.

=item RDA 4.22: Daniel Mortimer.

=item RDA 4.24: Greg Cook, Andrew Salt.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
