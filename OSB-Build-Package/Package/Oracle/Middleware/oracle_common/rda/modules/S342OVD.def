# S342OVD.def: Collects Oracle Virtual Directory Information
# $Id: S342OVD.def,v 2.11 2012/01/03 13:34:45 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S342OVD.def,v 2.11 2012/01/03 13:34:45 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S342OVD - Collects Oracle Virtual Directory Information

=head1 DESCRIPTION

This module collects Oracle Virtual Directory-related information (for example,
server, configuration, and log files).

The following reports can be generated and are regrouped under C<Oracle
Virtual Directory>:

=cut

echo tput('bold'),'Processing Oracle Virtual Directory module ...',tput('off')

# Initialization
var $ORACLE_HOME = getSetting('ORACLE_HOME','')

# Collect the Oracle Virtual Directory information
if !?getSetting('OVD_PRIMARY')
{# Initialization
 var $OVD_HOME = getSetting('OVD_HOME')

 if !$OVD_HOME
  var $OVD_HOME = $ORACLE_HOME

 var $TOC = '%TOC%'
 var $TOP = '[[#Top][Back to top]]'
 pretoc '1:Oracle Virtual Directory'

 # Load the common macros
 run library()

=head2 Server Files

Collects the following server files:

=over

=item o F<ovidserver.lax>

=item o F<vde.sh>

=item o F<vde_start.sh>

=back

=cut

 debug ' Inside OVD module, gathering server files'
 pretoc '2:Server Files'
 var $dir = $OVD_HOME
 call cat_report($dir,'ovidserver.lax','s_')
 call cat_report($dir,'vde.sh','s_')
 call cat_report($dir,'vde_start.sh','s_')
 unpretoc

=head2 Configuration Files

Collects configuration files from the F<conf> directory.

=cut

 debug ' Inside OVD module, gathering configuration files'
 pretoc '2:Configuration Files'
 var $dir = catDir($OVD_HOME,'conf')
 loop $fil (grepDir($dir,'.*','n'))
  call cat_report($dir,$fil,'c_')
 unpretoc

=head2 Log Files

Collects the log files from the F<log> directory.

=cut

 debug ' Inside OVD module, gathering log files'
 pretoc '2:Log Files'
 var $dir = catDir($OVD_HOME,'log')
 call cat_report($dir,'access.log','l_')
 call cat_report($dir,'vde.log','l_')
 call cat_report($dir,'vde.log.exception','l_')
 call cat_report($dir,'vde_startup.log','l_')
 unpretoc

=head2 not_found - Not Found

Used as a warning when none of the Oracle Virtual Directory files are found.

=cut

 if !isTocCreated(true)
 {debug ' Inside OVD module, no file found'
  report not_found
  write 'No Oracle Virtual Directory files were found and all files were \
         optional. This does not mean there was necessarily a problem with \
         RDA, but it should be investigated.'
  toc $MNU
  toc '2:[[',getFile(),'][rda_report][Not Found]]'
 }
}

=head1 VIRTUAL DIRECTORY INFORMATION FOR FUSION MIDDLEWARE

Includes the reports generated by the IREQ module about the Oracle instances
and their associated Oracle homes.

=cut

elsif getSetting('OVD_REQ_IREQ')
{# Collect the OVD 11g information
 toc '%PUSH("0:   * Oracle Virtual Directory")%'
 toc '%PUSH("1+:Common Product Home")%'
 toc '%INCLUDE("',${CUR.GROUP},'_IREQ_OVD_CH.toc")%'
 toc '%POP%'
 loop $key (grepSetting('^OVD_OI\d+_REQ_INSTANCE$'))
 {var $tid = substr($key,0,-13)
  var $ins = basename(getSetting($key,''))
  toc '%PUSH("0:      * ',"'",$ins,"'",' Instance")%'
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("1++:Associated Oracle Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',replace($tid,'_OI','_OH'),'.toc",1)%'
  toc '%POP2%'
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("1++:Instance Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',$tid,'.toc")%'
  toc '%POP3%'
 }
 toc '%POP%'
}

=head1 SEE ALSO

L<S313IREQ.def|modules::S313IREQ>,
L<library.def|modules::library>,
L<OVDr11.def|modules::OVDr11>

=begin credits

=over 10

=item RDA 4.9:  Mark Wilcox.

=item RDA 4.15: Greg Cook, Andrew Salt, Olaf Stullich.

=item RDA 4.22: Daniel Mortimer.

=item RDA 4.24: Greg Cook, Andrew Salt.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
