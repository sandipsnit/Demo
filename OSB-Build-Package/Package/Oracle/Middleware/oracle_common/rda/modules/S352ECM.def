# S352ECM.def: Controls Oracle Enterprise Content Management Data Collection
# $Id: S352ECM.def,v 2.8 2012/01/23 12:29:34 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S352ECM.def,v 2.8 2012/01/23 12:29:34 mschenke Exp $
#
# Change History
# 20120120  KRA  Improve documentation.

=head1 NAME

S352ECM - Controls Oracle Enterprise Content Management Data Collection

=head1 DESCRIPTION

This module defines common settings for all Oracle Enterprise Content
Management modules and gathers information that is common for Oracle
Enterprise Content Management components.

Oracle Enterprise Content Management includes:

=over 3

=item o Distributed Document Capture

=item o Document Capture

=item o Imaging and Process Management

=item o Information Rights Management

=item o Universal Content Management

=item o Universal Online Archive

=item o Universal Records Management

=back

The following reports can be generated and are regrouped under C<Enterprise
Content Management>:

=head1 REPORTS

=cut

echo tput('bold'),'Processing ECM module ...',tput('off')

# Initialization
var $ORACLE_HOME = catDir(getSetting('ORACLE_HOME',''))
var $TAIL        = getSetting('RDA_TAIL',1000)

var $ASSISTANTS_HOME  = catDir($ORACLE_HOME,'assistants')
var $CFGTOOLLOGS_HOME = catDir($ORACLE_HOME,'cfgtoollogs')
var $ECM_JDK          = catDir($ORACLE_HOME,'jdk')
var $INSTALL_HOME     = catDir($ORACLE_HOME,'install')

var $OH  = quote($ORACLE_HOME)
var $OH1 = concat('^',$OH,'[\\\/]')
var $OH2 = concat('^',$OH,'\b')

var $MNU = 'Enterprise Content Management'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '1:',$MNU

# Load the common macros
run library()
run WLSlib()

=head1 ORACLE HOME COLLECTIONS

=head2 Install and Configuration

Gathers the Oracle Fusion Middleware configuration and installation files.

=cut

debug ' Inside ECM module, gathering install and configuration information'
pretoc '2:Installation and Configuration'
call sort_files(3,$TAIL,\
  grepDir($ASSISTANTS_HOME,'\.log$','dir'),\
  grepDir($CFGTOOLLOGS_HOME,'\.(ini|log|properties|xml)','dir'),\
  grepDir($INSTALL_HOME,'\.(ini|log)','dir'))
unpretoc

if testDir('d',catDir($ORACLE_HOME,'diagnostics'))
{debug ' Inside ECM module, gathering diagnostics information'
 pretoc '2:Diagnostics'
 call sort_files(3,$TAIL,\
   grepDir(lastDir(),'\.((conf|ini|xml|properties|err)$|log)|\~','dir'))
 unpretoc
}

=head2 jdk_info - JDK Information

Checks for the available JDK and reports its version.

=cut

debug ' Inside ECM module, gathering jar files'
report jdk_info
prefix
{write '---+!! JDK Information'
 write $TOC
}
loop $cmd (grepDir($ECM_JDK,'^java(\.exe)?$','inr'))
{if testFile('fx',$cmd)
 {write '---++ JDK Location: ',encode(replace($cmd,$OH2,'$OH'))
  call statFile('p',$cmd)
  var $cmd = quote($cmd,'x')
  write '---++++!! JDK Version'
  call writeCommand(concat($cmd,' -version 2>&1'))
  write '---++++!! JDK Full Version'
  call writeCommand(concat($cmd,' -fullversion 2>&1'))
  write $TOP
 }
}
if isCreated(true)
 toc '2:[[',getFile(),'][rda_report][JDK Information]]'

=head2 jdk_libs - Java Libs

Gathers information about all C<*.jar> files located in the common directory.
For each Jar file, the report collects file information and the output from
C<cksum>.

=cut

debug ' Inside ECM module, gathering Java libs information'
report java_libs
prefix
{write '---+!! Information about Java Libs'
 write '%TOC3-2%'
}
var $cmd = findCommand('cksum')
loop $jar (grepDir(catDir($ORACLE_HOME,'j2ee','home'),'\.jar$','inr'),\
           grepDir(catDir($ORACLE_HOME,'jlib'),'\.jar$','inr'),\
           grepDir(catDir($ORACLE_HOME,'lib'),'\.jar$','inr'),\
           grepDir(catDir($ORACLE_HOME,'jdbc','lib'),'\.(jar|zip)$','dir'))
{if testFile('f',$jar)
 {if match($jar,$OH2)
   write '---++ ',encode(replace($jar,$OH2,'$OH'))
  else
   write '---++ ',encode($jar)
  call statFile('p',$jar)
  if $cmd
  {write '---++++!! cksum: (checksum size filename)'
   call writeCommand(concat($cmd,' ',quote($jar,'x')))
  }
  write $TOP
 }
}
if isCreated(true)
 toc '2:[[',getFile(),'][rda_report][Java Libs]]'

=head2 Manifest Information

Gathers the Oracle home manifest information (on version 11.x and later).

=cut

if getSetting('ECM_PRIMARY')
{debug ' Inside ECM module, getting Oracle home manifest information \
         (can take time)'
 pretoc '2:Manifest Information'
 call dsp_manifest(3,$ORACLE_HOME)
 unpretoc
}

=head1 COMMON HOME INFORMATION

It includes the reports generated by the IREQ module about the common home (on
version 11.x and later).

=cut

toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Common Product Home")%'
toc '%INCLUDE("',${CUR.GROUP},'_IREQ_ECM_CH.toc")%'
toc '%POP2%'

=head1 ORACLE WEBLOGIC SERVER DOMAIN COLLECTIONS

It includes all reports produced by the WREQ module for the specified Oracle
WebLogic Server domain (on version 11.x and later).

=cut

toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Overview")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_ECM_TOP.toc")%'
toc '%POP2%'
toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Domain")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_ECM_DOM.toc")%'
toc '%POP2%'

# Disable the group title in next index
if isTocCreated()
 toc '-:',$MNU

=head1 SEE ALSO

L<S301WREQ.def|modules::S301WREQ>,
L<S313IREQ.def|modules::S313IREQ>,
L<S353UOA.def|modules::S353UOA>,
L<S354UCM.def|modules::S354UCM>,
L<S355IPM.def|modules::S355IPM>,
L<S357ODC.def|modules::S357ODC>,
L<library.def|modules::library>,
L<WLSlib.def|modules::WLSlib>

=begin credits

=over 10

=item RDA 4.16: Greg Cook, Andrew Salt.

=item RDA 4.26: Cindi Canfield.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
