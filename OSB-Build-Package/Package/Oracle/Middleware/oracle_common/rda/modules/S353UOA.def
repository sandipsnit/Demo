# S353UOA.def: Collects Oracle Universal Online Archive Information
# $Id: S353UOA.def,v 2.9 2012/01/03 13:34:46 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S353UOA.def,v 2.9 2012/01/03 13:34:46 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S353UOA - Collects Oracle Universal Online Archive Information

=head1 DESCRIPTION

This module collects Oracle Universal Online Archive information, which
includes executable list, configuration, and log files. The module covers
Oracle Universal Online Archive Release 10.2.2.x and 11.x.

The following reports can be generated and are regrouped under C<Universal
Online Archive>:

=head1 REPORTS

=cut

echo tput('bold'),'Processing UOA module ...',tput('off')

# Initialization
var $ORACLE_HOME = catDir(getSetting('ORACLE_HOME',''))

var $MNU = 'Enterprise Content Management'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '^1:',$MNU
pretoc '1+:Universal Online Archive'

# UOA version 11g
if testDir('d',$UOA_HOME = catDir($ORACLE_HOME,'uoa'))
{# Initialization
 var $TAIL = getSetting('RDA_TAIL',1000)

 # Load the common macros
 run library()
 run WLSlib()

=head1 ORACLE HOME COLLECTIONS

=head2 repos_info - Repository

Collects Universal Online Archive repository information such as repository
version, domain properties, node, service, and server configurations.

=cut

 run CONTinfo(true,true)

=head2 uoa_bin - Executable List

Gathers Universal Online Archive executable list.

=cut

 debug ' Inside UOA module, getting UOA executable list'
 var $dir = catDir($UOA_HOME,'bin')
 report uoa_bin
 prefix
  write '---+ List of Files in ',encode($dir)
 loop $fil ($dir,grepDir($dir,'^\.+$','vr'))
 {if testDir('d',$fil)
   call statDir('an',$fil)
 }
 if isCreated(true)
  toc '2:[[',getFile(),'][rda_report][Executable List]]'

=head2 Manifest Information

Gathers the Oracle home manifest information.

=cut

 if getSetting('UOA_PRIMARY')
 {debug ' Inside UOA module, getting Oracle home manifest information \
          (can take time)'
  pretoc '2:Manifest Information'
  call dsp_manifest(3,$ORACLE_HOME)
  unpretoc
 }

=head2 Configuration Files

Gathers Universal Online Archive-related configuration files.

=cut

 debug ' Inside UOA module, collecting the configuration files from Oracle home'
 pretoc '2:Configuration Files'
 call sort_files(3,$TAIL,\
                 grepDir(catDir($UOA_HOME,'admin','config'),'\.xml$','p'),\
                 grepDir(catDir($UOA_HOME,'settings'),'\.xml$','p'))
 unpretoc

=head1 COMMON HOME INFORMATION

Includes the reports generated by the IREQ module about the common home.

=cut

 toc '%PUSH("%SPLIT%")%'
 toc '%PUSH("1+:Common Product Home")%'
 toc '%INCLUDE("',${CUR.GROUP},'_IREQ_UOA_CH.toc")%'
 toc '%POP2%'

=head1 ORACLE WEBLOGIC SERVER DOMAIN COLLECTIONS

Includes all reports collected for the specified Oracle WebLogic Server domain.

=cut

 toc '%PUSH("%SPLIT%")%'
 toc '%PUSH("1+:Oracle WebLogic Server Overview")%'
 toc '%INCLUDE("',${CUR.GROUP},'_WREQ_UOA_TOP.toc")%'
 toc '%POP2%'
 toc '%PUSH("%SPLIT%")%'
 toc '%PUSH("1+:Oracle WebLogic Server Domain")%'
 toc '%INCLUDE("',${CUR.GROUP},'_WREQ_UOA_DOM.toc")%'
 toc '%POP2%'
}
elsif testDir('d',catDir($ORACLE_HOME,'content'))
{# Initialization
 var $TAIL1 = getSetting('UOA_TAIL1',5000)
 var $TAIL2 = getSetting('UOA_TAIL2',500)

 # Collect 10g content information
 run CONTinfo(false,true)
}

=head2 not_found - Not found

Used as a warning when none of the Oracle Universal Online Archive files
are found.

=cut

if !isTocCreated()
{debug ' Inside UOA module, no file found'
 report not_found
 write 'No Oracle Universal Online Archive configuration files were \
        found and all files were optional. This does not mean there was \
        necessarily a problem with RDA, but it should be investigated.'
 toc '2:[[',getFile(),'][rda_report][Not found]]'
}

# Disable the group title in next index
toc '-:',$MNU

=head1 SEE ALSO

L<S352ECM.def|modules::S352ECM>,
L<S313IREQ.def|modules::S313IREQ>,
L<S301WREQ.def|modules::S301WREQ>,
L<CONTinfo.def|modules::CONTinfo>,
L<library.def|modules::library>,
L<WLSlib.def|modules::WLSlib>

=begin credits

=over 10

=item RDA 4.16: Greg Cook, Andrew Salt.

=item RDA 4.24: Greg Cook, Andrew Salt.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
