# S354UCM.def: Collects Oracle Universal Content Management Information
# $Id: S354UCM.def,v 2.15 2012/07/24 13:30:04 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S354UCM.def,v 2.15 2012/07/24 13:30:04 mschenke Exp $
#
# Change History
# 20120709  KRA  Add 'DC Templates' section.

=head1 NAME

S354UCM - Collects Oracle Universal Content Management Information

=head1 DESCRIPTION

This module collects Oracle Universal Content Management-related information.

The following reports can be generated and are regrouped under C<Universal
Content Management>:

=head1 REPORTS

=cut

echo tput('bold'),'Processing UCM module ...',tput('off')

# Initialization
var $AGE  = getSetting('UCM_LOG_AGE',5)
var $TAIL = getSetting('RDA_TAIL',1000)

if testDir('d',catDir($ORACLE_HOME = getSetting('ORACLE_HOME',''),'ucm'))
{if $PRIMARY = getSetting('UCM_PRIMARY')
  var $DOMAIN  = getSetting('UCM_DOM_REQ_DOMAIN','')
 else
  var $DOMAIN  = getSetting('ECM_DOM_REQ_DOMAIN','')
 var $CS_HOME  = catDir($DOMAIN,'ucm','cs')
 var $IBR_HOME = catDir($DOMAIN,'ucm','ibr')
 var $URM_HOME = catDir($DOMAIN,'ucm','urm')
}
else
{var $CS_HOME   = getSetting('UCM_CS_HOME','')
 var $IBR_HOME  = getSetting('UCM_IBR_HOME','')
 var $SSPU_HOME = getSetting('UCM_SSPU_HOME','')
}

var $MNU = 'Enterprise Content Management'
var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'

pretoc '^1:',$MNU
pretoc '1+:Universal Content Management'

# Load the common macros
run library()
run WLSlib()

=head2 Content Server

Collects Content Server-related information.

=cut

if $CS_HOME
{debug ' Inside UCM module, getting content server configuration files'
 pretoc '2:Content Server'

 # Collect the configuration files
 pretoc '3:Configuration Files'
 call sort_files(4,$TAIL,\
  catFile($CS_HOME,'bin','intradoc.cfg'),\
  catFile($CS_HOME,'config','config.cfg'),\
  catFile($CS_HOME,'data','conversion','cvtemplates.hda'),\
  grepDir(catDir($CS_HOME,'data','configuration'),'^\.+$','drv'),\
  grepDir(catDir($CS_HOME,'custom','PDFConverter','bin','win32'),\
          '^\.+$','drv'))
 unpretoc

 # Collect the log files
 pretoc '3:Log Files'
 call sort_files(4,0,\
  grepDir(catDir($CS_HOME,'weblayout','groups','secure','logs'),\
          '\.htm$',concat('irm',$AGE)))
 unpretoc

 # Collect the trace log files
 pretoc '3:Trace Log Files'
 call sort_files(4,0,\
  grepDir(catDir($CS_HOME,'data','trace'),'\.log$',concat('ipm',$AGE)))
 unpretoc

 # Collect the event log files
 pretoc '3:Event Log Files'
 call sort_files(4,0,\
  grepDir(catDir($CS_HOME,'data','trace','event'),'\.log$',concat('ipm',$AGE)))
 unpretoc

=head3 env_info - Environment Information

For UNIX, collects Dynamic Converter environment information.

=cut

 if isUnix()
 {debug ' Inside UCM module, getting dynamic converter environment information'
  report env_info
  write '---+!! Dynamic Converter Environment Information'
  write $TOC

  # Get the environment variables
  write '---+ Dynamic Converter Environment Variables'
  write '|*Variable*|*Value*|'
  loop $key ('DISPLAY','PATH',@{CUR.SHLIB})
   write '|',$key,'|',getEnv($key),' |'
  write $TOP

  # Get the locale information
  if match(getOsName(),'^(aix|hpux|linux|solaris|sunos)$')
  {if loadCommand('locale')
   {prefix
    {write '---+ Locale Information'
     write '|*Name*|*Value*|'
    }
    loop $lin (getLines())
     write '|',key($lin),' |',value($lin),' |'
    if hasOutput(true)
     write $TOP
   }
  }
  toc '3:[[',getFile(),'][rda_report][Environment Information]]'
 }
 unpretoc
}

=head2 Inbound Refinery

Collects Inbound Refinery-related information.

=cut

if $IBR_HOME
{debug ' Inside UCM module, getting IBR configuration and log files'
 pretoc '2:Inbound Refinery'

 # Collect the configuration files
 pretoc '3:Configuration Files'
 call sort_files(4,$TAIL,\
  catFile($IBR_HOME,'config','config.cfg'),\
  catFile($IBR_HOME,'bin','intradoc.cfg'),\
  grepDir(catDir($IBR_HOME,'data','configuration'),'^\.+$','drv'),\
  grepDir(catDir($IBR_HOME,'custom','PDFConverter','bin','win32'),\
          '^\.+$','drv'))
 unpretoc

 # Collect the log files
 pretoc '3:Log Files'
 call sort_files(4,0,\
  catFile($IBR_HOME,'bin','IdcRefineryNT.log'),\
  grepDir(catDir($IBR_HOME,'weblayout','groups','secure','logs'),\
          '\.htm$',concat('dirm',$AGE)))
 unpretoc 2

=head2 Digital Asset Management

Collects Digital Asset Management-related information.

=cut

 debug ' Inside UCM module, getting DAM configuration and log files'
 pretoc '2:Digital Asset Management'

 # Collect the configuration files
 pretoc '3:Configuration Files'
 call sort_files(4,$TAIL,\
  grepDir(catDir($IBR_HOME,'data','configuration','dam'),'^\.+$','drv'))
 unpretoc 2
}

=head2 Universal Records Management

Collects Universal Records Management-related information.

=cut

if $URM_HOME
{debug ' Inside UCM module, getting URM configuration files'
 pretoc '2:Universal Records Management'

 # Collect the configuration files
 pretoc '3:Configuration Files'
 call sort_files(4,$TAIL,\
   catFile($URM_HOME,'config','config.cfg'),\
   catFile($ORACLE_HOME,'ucm','idc','components','RecordsManagement',\
           'records_management_environment.cfg'),\
   catFile($ORACLE_HOME,'ucm','idc','components','PhysicalContentManager',\
           'physicalcontentmanager_environment.cfg'))
 unpretoc 2
}

=head2 Web Content Management

=head3 Site Studio Publishing Utility

Collects Site Studio Publishing Utility-related information.

=cut

if $SSPU_HOME
{debug ' Inside UCM module, getting SSPU configuration and log files'
 pretoc '2:Web Content Management'
 pretoc '3:Site Studio Publishing Utility'

 # Collect the configuration files
 pretoc '4:Configuration Files'
 call sort_files(5,$TAIL,\
  catFile($SSPU_HOME,'sitestudio.config'),\
  grepDir($SSPU_HOME,'^cns\.(db2|oracle|sqlserver)\.config$','ip'))
 unpretoc

 # Collect the log files
 pretoc '4:Log Files'
 call sort_files(5,$TAIL,catFile($SSPU_HOME,'syndicator.log'))
 unpretoc 3
}

=head1 ORACLE HOME INFORMATION

=head2 Manifest Information

Gathers the Oracle home manifest information.

=cut

if and($PRIMARY,length($ORACLE_HOME))
{debug ' Inside UCM module, getting Oracle home manifest information \
         (can take time)'
 pretoc '2:Manifest Information'
 call dsp_manifest(3,$ORACLE_HOME)
 unpretoc

=head2 dc_templates - DC Templates

Gathers the dynamic converter template files.

=cut

 debug ' Inside UCM module, getting dynamic converter templates'
 report dc_templates
 prefix
 {write '---+ Dynamic Converter Templates Files'
  write '   * Links point to files that have been collected in their \
              original format. Opening them directly in your browser \
              can present risks. To prevent them, access the file \
              outside the browser or use the link to save them and use \
              an adequate viewer.'
  write '|*File Name*| *Size*|*Last Modified Date*|'
 }
 loop $fil (grepDir(catDir($ORACLE_HOME,'ucm','Distribution',\
                           'DynamicConverterTemplates'),\
                    '^\.+$','drv'))
 {if testFile('fr',$fil)
  {var $lnk = encode(addSymbol($fil))
   var $siz = getSize($fil)
   if $siz
   {output d,concat('dct_',basename($fil))
    if ${CUR.LAST}->write_data($fil)
     var $lnk = concat('[[',${CUR.LAST}->get_raw(true),'][_blank][',$lnk,']]')
    end ${CUR.LAST}
   }
   write '|',$lnk,' | ',$siz,'|',\
             getLastModify($fil,'%d-%b-%Y&nbsp;%H:%M:%S'),' |'
  }
 }
 if isCreated(true)
 {write $TOP
  toc '2:[[',getFile(),'][rda_report][DC Templates]]'
 }
}

=head1 COMMON HOME INFORMATION

Includes the reports generated by the IREQ module about the common home.

=cut

toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Common Product Home")%'
toc '%INCLUDE("',${CUR.GROUP},'_IREQ_UCM_CH.toc")%'
toc '%POP2%'

=head1 ORACLE WEBLOGIC SERVER DOMAIN COLLECTIONS

Includes all reports collected for the specified Oracle WebLogic Server domain.

=cut

toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Overview")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_UCM_TOP.toc")%'
toc '%POP2%'
toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Domain")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_UCM_DOM.toc")%'
toc '%POP2%'

# Disable the group title in next index
toc '-:',$MNU

=head1 SEE ALSO

L<S301WREQ.def|modules::S301WREQ>,
L<S313IREQ.def|modules::S313IREQ>,
L<S352ECM.def|modules::S352ECM>,
L<library.def|modules::library>,
L<WLSlib.def|modules::WLSlib>

=begin credits

=over 10

=item RDA 4.17: Jeremy Reilmann, Matt Rudd.

=item RDA 4.21: Jeremy Reilmann.

=item RDA 4.24: Greg Cook, Andrew Salt.

=item RDA 4.28: Alan Boucher.

=item RDA 4.29: Alan Boucher, Thrond Toftley.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
