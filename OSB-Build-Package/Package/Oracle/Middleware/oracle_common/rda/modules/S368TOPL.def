# S368TOPL.def: Collects Oracle TopLink Information
# $Id: S368TOPL.def,v 2.10 2012/08/29 13:52:46 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S368TOPL.def,v 2.10 2012/08/29 13:52:46 mschenke Exp $
#
# Change History
# 20120828  KRA  Extend installation type detection.

=head1 NAME

S368TOPL - Collects Oracle TopLink Information

=head1 DESCRIPTION

This module collects Oracle TopLink-related diagnostic information.

The following reports can be generated and are regrouped under C<Oracle
TopLink>:

=cut

echo tput('bold'),'Processing Oracle TopLink module ...',tput('off')

# Initialization
var $ORACLE_HOME   = getSetting('ORACLE_HOME','')
var $ORACLE_PARENT = getSetting('ORACLE_PARENT',catDir($ORACLE_HOME,upDir()))
var $TOPLINK_JLIB  = catDir($ORACLE_HOME,'toplink','jlib')

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '1:Oracle TopLink'

# Identify the product version
if or(first(grepDir($ORACLE_PARENT,'^wlserver_\d+','fp')),\
      match(getSetting('INSTALLATION_TYPE',''),\
            '^(Classic Application Server 11g|\
               Oracle Forms and Reports 11g|\
               Oracle Identity Management 11g|\
               Oracle Portal, Forms, Reports and Discoverer 11g|\
               Oracle SOA Suite 11g|\
               Oracle WebCenter Portal Suite 11g|\
               Oracle WebCenter Suite 11g|\
               Oracle WebTier|\
               SOA Application Server 11g|\
               Webcenter Application Server 11g)'),\
      testDir('d',catDir($ORACLE_HOME,'rcu')))
 var $AS11 = true

=head2 toplinklib - Oracle TopLink Libraries

Lists the Oracle TopLink libraries present in the F<$MW_HOME> directory
structure.

=cut

if $AS11
{debug ' Inside TOPL module, listing the TopLink libraries (can take time)'
 report toplinklib
 prefix
  write '---+ Oracle TopLink Libraries'
 call statFile('p',grepDir(catDir($ORACLE_PARENT,'modules'),\
                           '(link|persistence).*\.jar$','dir'))
 if isCreated(true)
  toc '2:[[',getFile(),'][rda_report][Oracle TopLink Libraries]]'
}
else
{

=head2 manifest - Manifest Information

When F<unzip> is available, it extracts the manifest from the following files
located in the F<$ORACLE_HOME/toplink/jlib> directory:

=over 4

=item o F<tljaxb.jar>

=item o F<tlmwcore.jar>

=item o F<toplink.jar>

=item o F<toplink-deprecated.jar>

=item o F<toplink-oc4j.jar>

=back

=cut

 if findCommand('unzip')
 {var $pgm = last
  report manifest
  loop $fil ('tljaxb.jar',\
             'tlmwcore.jar',\
             'toplink.jar',\
             'toplink-deprecated.jar',\
             'toplink-oc4j.jar')
  {next !testFile('f',catFile($TOPLINK_JLIB,$fil))
   debug ' Inside TOPL module, extracting ',$fil,' manifest'
   prefix
   {if !isCreated()
    {write '---+!! Manifest Information'
     write $TOC
    }
    write '---+ ',$fil,' Manifest'
   }
   var $cmd = concat($pgm,' -p ',lastCommand(),' META-INF/MANIFEST.MF')
   call writeCommand($cmd)
   if hasOutput(true)
    write $TOP
  }
  if isCreated()
   toc '2:[[',getFile(),'][rda_report][Manifest Information]]'
 }

=head2 jlib - jlib Directory Contents

Lists the contents of F<$ORACLE_HOME/toplink/jlib> directory.

=cut

 debug ' Inside TOPL module, listing the jlib directory contents'
 report jlib
 prefix
  write '---+ Contents of the jlib Directory'
 call statDir('n',$TOPLINK_JLIB)
 if isCreated(true)
  toc '2:[[',getFile(),'][rda_report][jlib Directory Contents]]'
}
unpretoc

=begin credits

=over 10

=item RDA 4.17: Anjani Sankar, Miguel Hernandez.

=item RDA 4.20: Miguel Hernandez.

=item RDA 4.29: Adriana Dominguez, Meraj Mohammed.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
