# S397BI.def: Collects Oracle Business Intelligence Enterprise Edition Info.
# $Id: S397BI.def,v 2.12 2012/01/03 13:34:46 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S397BI.def,v 2.12 2012/01/03 13:34:46 mschenke Exp $
#
# Change History
# 20120103  MSC  Change the copyright notice.

=head1 NAME

S397BI - Collects Oracle Business Intelligence Enterprise Edition Information

=head1 DESCRIPTION

This module collects information related to Oracle Business Intelligence
Enterprise Edition, formerly known as Siebel Business Analytics.

The following reports can be generated and are regrouped under C<Oracle
Business Intelligence Enterprise Edition>:

=cut

echo tput('bold'),'Processing BI module ...',tput('off')

# Initialization
var $ORACLE_HOME = getSetting('ORACLE_HOME','')

=head1 BUSINESS INTELLIGENCE ENTERPRISE EDITION INFORMATION

Includes the reports generated by the IREQ module about the Oracle instances
and their associated Oracle homes.

=cut

if or(getSetting('BI_REQ_IREQ'),\
      testDir('d',catDir($ORACLE_HOME,'bifoundation')),\
      testDir('d',catDir($ORACLE_HOME,'rcu')))
{# Collect the BI 11g information
 toc '%PUSH("0:   * Oracle Business Intelligence Enterprise Edition")%'
 toc '%PUSH("1+:Common Product Home")%'
 toc '%INCLUDE("',${CUR.GROUP},'_IREQ_BI_CH.toc")%'
 toc '%POP%'
 loop $key (grepSetting('^BI_OI\d+_REQ_INSTANCE$'))
 {var $tid = substr($key,0,-13)
  var $ins = basename(getSetting($key,''))
  toc '%PUSH("0:      * ',"'",$ins,"'",' Instance")%'
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("1++:Associated Oracle Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',replace($tid,'_OI','_OH'),'.toc",1)%'
  toc '%POP2%'
  toc '%PUSH("%SPLIT%")%'
  toc '%PUSH("1++:Instance Home")%'
  toc '%INCLUDE("',${CUR.GROUP},'_IREQ_',$tid,'.toc")%'
  toc '%POP3%'
 }
 toc '%POP%'
}
else
{# Initialization
 var $BI_AGE  = getSetting('BI_LOG_AGE',5)
 var $BI_DATA = getSetting('BI_DATA','')
 var $BI_HOME = getSetting('BI_HOME','')
 var $BI_TAIL = getSetting('BI_TAIL',5000)

 var $TOC = '%TOC%'
 var $TOP = '[[#Top][Back to top]]'
 pretoc '1:Oracle Business Intelligence Enterprise Edition'

 # Load the common macros
 run library()

=head2 bi_version - Version Information

Gathers version information.

=cut

 debug ' Inside BI module, getting version information'
 report bi_version
 call cat_file(catDir($BI_HOME,'server','Document'),'version.txt')
 if isCreated(true)
  toc '2:[[',getFile(),'][rda_report][Version Information]]'

=head2 present_config - Presentation Server

Gathers presentation server configuration information.

=cut

 pretoc '2:Configuration Files'

 debug ' Inside BI module, getting presentation server configuration'
 report present_config
 call cat_file(catDir($BI_DATA,'web','config'),'instanceconfig.xml')
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Presentation Server]]'

=head2 sched_config - Scheduler

Gathers scheduler configuration information.

=cut

 debug ' Inside BI module, getting scheduler configuration'
 report sched_config
 call cat_file(catDir($BI_DATA,'scheduler','config'),'instanceconfig.xml')
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Scheduler]]'

=head2 server_config - Server

Gathers server configuration information.

=cut

 debug ' Inside BI module, getting server configuration'
 report server_config
 call cat_file(catDir($BI_HOME,'server','Config'),'NQSConfig.INI')
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Server]]'

=head2 cluster_config - Cluster Controller

Gathers cluster controller configuration information.

=cut

 debug ' Inside BI module, getting cluster controller configuration'
 report cluster_config
 call cat_file(catDir($BI_HOME,'server','Config'),'NQClusterConfig.INI')
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Cluster Controller]]'
 unpretoc

=head2 Log Files

Gathers important log files.

=cut

 pretoc '2:Log Files'

 debug ' Inside BI module, getting server log'
 report server_log
 call tail_file(catDir($BI_HOME,'server','Log'),'NQServer.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Server]]'

 debug ' Inside BI module, getting query log'
 report query_log
 call tail_file(catDir($BI_HOME,'server','Log'),'NQQuery.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Query]]'

 debug ' Inside BI module, getting scheduler log'
 report scheduler_log
 call tail_file(catDir($BI_HOME,'server','Log'),'NQScheduler.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Scheduler]]'

 debug ' Inside BI module, getting administration tool log'
 report admin_log
 call tail_file(catDir($BI_HOME,'server','Log'),'NQSAdminTool.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Administration Tool]]'

 debug ' Inside BI module, getting install log'
 report install_log
 call tail_file(catDir($BI_HOME,'log'),'install.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Install]]'

 debug ' Inside BI module, getting cluster controller log'
 report cluster_log
 call tail_file(catDir($BI_HOME,'server','Log'),'NQCluster.log',$BI_TAIL)
 if isCreated(true)
  toc '3:[[',getFile(),'][rda_report][Cluster Controller]]'

 pretoc '3:Presentation Server'
 var $dir = catDir($BI_DATA,'web','log')
 report concat
 loop $fil (grepDir($dir,'^sawlog.*\.log$',concat('im',$BI_AGE)),\
            'sawserver.out.log')
 {report concat('web_',$fil)
  call tail_file($dir,$fil,$BI_TAIL)
  if isCreated(true)
   toc '4:[[',getFile(),'][rda_report][',encode($fil),']]'
 }
 unpretoc

 pretoc '3:Javahost'
 var $dir = catDir($BI_DATA,'web','log','javahost')
 loop $fil (grepDir($dir,'^jhost0\.log\.',concat('im',$BI_AGE)))
 {report concat('java_',$fil)
  call tail_file($dir,$fil,$BI_TAIL)
  if isCreated(true)
   toc '4:[[',getFile(),'][rda_report][',encode($fil),']]'
 }
 unpretoc 2

=head2 repository - Repositories

Gathers repositories.

=cut

 var $rep = getSetting('BI_REPOSITORY','All')
 if compare('eq',$rep,'All')
  var @fil = grepDir(catDir($BI_HOME,'server','Repository'),'\.rpd$','inp')
 elsif compare('eq',$rep,'None')
  var @fil = ()
 else
  var @fil = (catFile($BI_HOME,'server','Repository',$rep))
 run BIinfo(2,@fil)
 unpretoc
}

=head1 SEE ALSO

L<S313IREQ.def|modules::S313IREQ>,
L<library.def|modules::library>,
L<BIinfo.def|modules::BIinfo>,
L<BIr11.def|modules::BIr11>

=begin credits

=over 10

=item RDA 4.11: Ravindrasinh Parmar.

=item RDA 4.15: John Whitaker.

=item RDA 4.16: Greg Cook, Andrew Salt.

=item RDA 4.20: Mitra Veluri.

=item RDA 4.24: Greg Cook, Andrew Salt.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
