# S492PDC.def: Collects Oracle Communications Pricing Design Center Information
# $Id: S492PDC.def,v 1.1 2012/04/17 13:37:06 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S492PDC.def,v 1.1 2012/04/17 13:37:06 mschenke Exp $
#
# Change History
# 20120404  KRA  Initial version.

=head1 NAME

S492PDC - Collects Oracle Communications Pricing Design Center Information

=head1 DESCRIPTION

This module collects Oracle Communications Pricing Design Center-related
information. The following reports can be generated and are regrouped under
C<Pricing Design Center>:

=head1 REPORTS

=cut

echo tput('bold'),'Processing PDC module ...',tput('off')

# Initialization
var $HOME = getSetting('PDC_HOME','')
var $TAIL = getSetting('RDA_TAIL',1000)

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '1:Pricing Design Center'

# Set the current target
call setCurrent(addTarget('OH_PDC',{ORACLE_HOME=>$HOME,\
                                    OH_ABBR    =>'$HOM'}))

# Load the common macros
run library()

=head2 abbr - Abbreviations

Displays the RDA abbreviations defined for the Pricing Design Center home
collection.

=cut

debug ' Inside PDC module, collecting defined abbreviations'
report abbr
prefix
{write '---+ Pricing Design Center Home Abbreviations'
 write '|*Abbreviation*|*Location*|'
}
var %hsh = getSymbols()
loop $key (keys(%hsh))
 write '|',$key,' |',catDir(@{$hsh{$key}}),' |'
if isCreated(true)
 toc '2:[[',getFile(),'][rda_report][Abbreviations]]'

=head2 Configuration Files

Gathers Pricing Design Center-related configuration files.

=cut

debug ' Inside PDC module, gathering configuration files'
pretoc '2:Configuration Files'
var $cfg = catFile($HOME,'apps','transformation',\
                   'TransformationConfiguration.xml')
var ($dir,$nam) = parseFile($cfg)
call cat_report($dir,$nam)
unpretoc

=head2 Log Files

Gathers Pricing Design Center-related log files.

=cut

debug ' Inside PDC module, gathering log files'
if testFile('fr',$cfg)
{var (@dir,@tbl) = ()
 if xmlFind(xmlLoadFile($cfg),'Configuration/transformationLog')
 {var ($xml) = last
  if xmlFind($xml,'rreTransformLogFileLocation')
   call push(@dir,catDir(xmlData(last)))
  if xmlFind($xml,'breTransformLogFileLocation')
   call push(@dir,catDir(xmlData(last)))
 }

 # Get the log files
 loop $dir (@dir)
  call push(@tbl,grepDir($dir,'^\.+$','drv'))

 pretoc '2:Log Files'
 call sort_files(3,$TAIL,@tbl)
 unpretoc
}

=head2 Oracle WebLogic Server Domain Information

It includes all reports produced by the WREQ module for the specified Oracle
WebLogic Server domain.

=cut

toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Overview")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_PDC_TOP.toc")%'
toc '%POP2%'
toc '%PUSH("%SPLIT%")%'
toc '%PUSH("1+:Oracle WebLogic Server Domain")%'
toc '%INCLUDE("',${CUR.GROUP},'_WREQ_PDC_DOM.toc")%'
toc '%POP2%'

# Restore the default target
call setCurrent()

unpretoc

=head1 SEE ALSO

L<S301WREQ.def|modules::S301WREQ>,
L<library.def|modules::library>

=begin credits

=over 10

=item RDA 4.28:  Nameer Jalel, See Tan, Dachywan Wu.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
