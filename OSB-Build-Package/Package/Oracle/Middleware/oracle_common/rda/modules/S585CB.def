# S585CB.def: Collects Oracle Crystal Ball Information
# $Id: S585CB.def,v 1.1 2012/07/30 14:33:26 mschenke Exp $
# ARCS: $Header: /home/cvs/cvs/RDA_4/src/scripting/lib/modules/S585CB.def,v 1.1 2012/07/30 14:33:26 mschenke Exp $
#
# Change History
# 20120620  KRA  Initial version.

=head1 NAME

S585CB - Collects Oracle Crystal Ball Information

=head1 DESCRIPTION

This module collects information for Oracle Crystal Ball.

The following reports can be generated and are regrouped under C<Oracle Crystal
Ball>:

=head1 REPORTS

=cut

echo tput('bold'),'Processing CB module ...',tput('off')

# Initialization
var $ROTATED_LOGS = getSetting('CB_ROTATED_LOGS',3)
var $TAIL         = getSetting('RDA_TAIL',1000)

var $TOC = '%TOC%'
var $TOP = '[[#Top][Back to top]]'
pretoc '1:Oracle Crystal Ball'

# Load the common library
run library()

=head2 Installation Log Files

Gathers Oracle Crystal Ball installation log files.

=cut

debug ' Inside CB module, gathering install log files'
pretoc '2:Installation Log Files'
call tail_report(${ENV.TEMP},'cbmsiinstall.txt',$TAIL)
call tail_report(${ENV.TEMP},'SystemAnalyzer.log',$TAIL)
unpretoc

=head2 Application Log Files

Gathers Oracle Crystal Ball application log files.

=cut

debug ' Inside CB module, gathering application log files'
var ($dir,%log) = (catDir(${ENV.APPDATA},'Oracle','Crystal Ball'))
loop $sub (findDir($dir,'^\d+','n'))
{next !testDir('d',$log = catDir($dir,$sub,'Logs'))
 loop $fil (grepDir($log,'^(AppMgr\s+log|CB\s+Log|PSI\s+Log)\.txt$','np'))
  var $log{$sub,$fil} = true
 if $ROTATED_LOGS
 {loop $pat ('^AppMgr\s+log_\d+\.txt$',\
             '^CB\s+Log_\d+\.txt$',\
             '^PSI\s+Log_\d+\.txt$')
  {var $cnt = $ROTATED_LOGS
   loop $fil (grepDir($log,$pat,'pt'))
   {var $log{$sub,$fil} = true
    break !decr($cnt)
   }
  }
 }
 if grepDir($log,'^\d{1,2}-\d{1,2}-\d{4}_\d+\.txt$','p')
  var $log{$sub,first(last)} = true
}
pretoc '2:Application Log Files'
loop $ver (keys(%log))
{pretoc "3:'",$ver,"' Version"
 call sort_files(4,0,keys($log{$ver}))
 unpretoc
}
unpretoc 2

=head1 SEE ALSO

L<library.def|modules::library>

=begin credits

=over 10

=item RDA 4.29: Aaron Weber.

=back

=end credits

=head1 COPYRIGHT NOTICE

Copyright (c) 2002, 2012, Oracle and/or its affiliates. All rights reserved.

=head1 TRADEMARK NOTICE

Oracle and Java are registered trademarks of Oracle and/or its
affiliates. Other names may be trademarks of their respective owners.

=cut
